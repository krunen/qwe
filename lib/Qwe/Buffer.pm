unit class Qwe::Buffer;
use Qwe::View;

has @!lines;
has $.filename;
has $.name;
has $.syntax;
has $.cfg;
has @!meta;
has @!undo;

method line($i) { @!lines[$i] // '' }
method numlines { +@!lines }
method line-meta($i) { @!meta[$i] // {} }

method insert-chars($x, $y, $s) {
  self.extend-to-line($y);
  @!lines[$y].substr-rw($x, 0) = $s;
}

method insert-chars-undo($x, $y, $s) {
  @!undo.push(sub {self.delete-chars($x, $y, $s.chars)});
  self.insert-chars($x, $y, $s);
}

method set-chars($x, $y, $s) {
  self.extend-to-line($y);
  @!lines[$y].substr-rw($x, $s.chars) = $s;
}

method set-chars-undo($x, $y, $s) {
  my $old = self.get-chars($x, $y, $s.chars);
  @!undo.push(sub { self.set-chars($x, $y, $old) });
  self.set-chars($x, $y, $s);
}

method get-chars($x, $y, $l) {
  @!lines[$y].substr($x, $l);
}

method delete-chars($x, $y, $l) {
  @!lines[$y].substr-rw($x, $l) = '';
}

method delete-chars-undo($x, $y, $l) {
  my $old = self.get-chars($x, $y, $l);
  @!undo.push(sub {self.insert-chars($x, $y, $old)});
  self.delete-chars($x, $y, $l);
}

method insert-line($y, $s, $meta?) {
  @!lines.splice($y, 0, $s);
  @!meta.splice($y, 0, $meta // {});
}

method insert-line-undo($y, $s) {
  @!undo.push(sub {self.delete-line($y)});
  self.insert-line($y, $s);
}

method delete-line($y) {
  @!lines.splice($y, 1);
  @!meta.splice($y,1);
}

method delete-line-undo($y) {
  my $old-s = self.line($y);
  my $old-m = self.line-meta($y);
  @!undo.push(sub {self.insert-line($y, $old-s, $old-m)});
  self.delete-line($y);
}

method extend-to-line($i) {
  for @!lines .. $i { @!lines.append('') }
}

method load-file($f) {
  die "Can't open file $f" unless $f.IO.e;

  @!lines = $f.IO.lines;
  $!filename = $f;
  $!name = $f.IO.basename;
}

method new-view(*%h) {
  Qwe::View.new(:buffer(self), |%h);
}

method meta($x,$y) {
  @!meta[$y]{$x} //= {};
  @!meta[$y]{$x}
}

method set-syntax($s) {
  if $!cfg.syntax($s) {
    $!syntax = $s;
  }
}

method undo() {
  .() given @!undo.pop;
}

sub wide_char_rx {
  # Generated by tools/widechars.pl6
  rx/<[\c[4352]..\c[4447]
       \c[9001]..\c[9002]
       \c[11904]..\c[11929]
       \c[11931]..\c[12019]
       \c[12032]..\c[12245]
       \c[12272]..\c[12283]
       \c[12289]..\c[12350]
       \c[12353]..\c[12438]
       \c[12441]..\c[12543]
       \c[12549]..\c[12589]
       \c[12593]..\c[12686]
       \c[12688]..\c[12730]
       \c[12736]..\c[12771]
       \c[12784]..\c[12830]
       \c[12832]..\c[12871]
       \c[12880]..\c[13054]
       \c[13056]..\c[19903]
       \c[19968]..\c[42124]
       \c[42128]..\c[42182]
       \c[43360]..\c[43388]
       \c[44032]..\c[55203]
       \c[63744]..\c[64255]
       \c[65040]..\c[65049]
       \c[65072]..\c[65106]
       \c[65108]..\c[65126]
       \c[65128]..\c[65131]
       \c[110592]..\c[110593]
       \c[127488]..\c[127490]
       \c[127504]..\c[127546]
       \c[127552]..\c[127560]
       \c[127568]..\c[127569]
       \c[131072]..\c[196605]
       \c[196608]..\c[262141]]>/;
}
